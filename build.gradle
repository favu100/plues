plugins {
    id 'groovy'
    id 'java'
    id 'application'
    id 'idea'
    id 'eclipse'
    id 'edu.sc.seis.launch4j' version '1.6.1'
    id "edu.sc.seis.macAppBundle" version "2.1.6"
}

sourceCompatibility = 8
targetCompatibility = 8

// https://coderwall.com/p/wuqopq/compile-groovy-before-java-in-gradle-build
sourceSets {
    main {
        groovy {
            // this makes the groovy-compiler compile groovy- as well
            // as java-files.
            // Needed, because java is normally compiled before groovy.
            // Since we are using groovy objects from java, we need it
            // the other way round.
            srcDirs = ['src/main/groovy', 'src/main/java']
        }
        java {
            srcDirs = [] // don't compile Java code twice
        }
    }
}

version = "2.0.0-SNAPSHOT"

mainClassName = "de.hhu.stups.plues.ui.Main"

applicationDefaultJvmArgs = ["-Dapple.awt.UIElement=true"]
def appName = "plues-" + project.version


repositories {
    mavenCentral()
    maven {
        name "snapshots"
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        name "releases"
        url "https://oss.sonatype.org/content/repositories/releases"
    }
}

dependencies {
    compile 'de.hhu.stups:de.prob2.kernel:2.0.1-SNAPSHOT'
    compile group: 'org.fxmisc.easybind', name: 'easybind', version: '1.0.3'
    compile 'org.controlsfx:controlsfx:8.40.11'
    compile 'com.google.code.findbugs:annotations:3.0.1'

    compile project(':model-generator')
    compile project(':musterstudienplaene')

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

launch4j {
    mainClassName = project.mainClassName
    outfile = appName + ".exe"
    version = project.version
    textVersion = project.version
    messagesStartupError = "Startup Error"
    messagesBundledJreError = "Bundled JRE Error"
    messagesLauncherError = "Launch Error"
    messagesJreVersionError = "JRE Version Error"
}

task winZip(dependsOn: "launch4j", type: Zip) {
    def outdir = "${project.buildDir}/launch4j" // project.launch4j.outputDir not working as expected
    def libdir = "${project.buildDir}/launch4j/${project.launch4j.libraryDir}"
    def zipdir = "${project.buildDir}/launch4j"

    from(outdir) {
        include project.launch4j.outfile
        into ''
    }

    from(libdir) {
        include "*.jar"
        into 'lib'
    }

    archiveName appName + "-win.zip"
    destinationDir file(zipdir)
}

macAppBundle.appName = appName
macAppBundle.mainClassName = project.mainClassName
